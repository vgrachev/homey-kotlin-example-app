buildscript {
    ext.kotlin_version = '1.2.30'

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
    }
}

apply plugin: 'kotlin2js'
apply plugin: 'kotlin-dce-js'
apply plugin: 'com.moowork.node'

node {
    // Replace with 'false' to use local node.js and npm
    download = true

    // Version of node to use.
    version = '8.9.4'

    // Version of npm to use.
    npmVersion = '5.7.1'
}

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
}

runDceKotlinJs.keep "homey-kotlin-example-app.com.vgrachev.homey.kotlin.app.MyApp"

compileKotlin2Js.kotlinOptions.moduleKind = "commonjs"

ext.configureNpmTask = { it.with {
    inputs.files(fileTree('node_modules'))
    inputs.files(fileTree('src'))
    inputs.file('package.json')
    inputs.file('webpack.config.js')

    outputs.dir('dist')
}}

task buildDevBundle(type: NpmTask, dependsOn: [npmInstall, runDceKotlinJs]) { task ->
    configureNpmTask task
    args = ["run", "bundle-dev"]
}

task buildBundle(type: NpmTask, dependsOn: [npmInstall, runDceKotlinJs]) { task ->
    configureNpmTask task
    args = ["run", "bundle"]
}

task appRun(type: NpmTask, dependsOn: buildDevBundle) {
    args = ["run", "app-run"]
}

assemble.dependsOn buildDevBundle